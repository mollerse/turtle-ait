# Load some dependencies
@load ait-canvas ;
@load ait-canvas/node ;
@load ./turtle ;

point: [] cons cons ;
deg2rad: PI 180 / * ;
dup2: dupd dup rolldown ;
centerCanvas: WMax HMax translate ;
clearCanvas: WMin HMin point W H fillRect ;

edge: turn move ;
spin: slices [ dup2 turn exec ] times pop pop ;
polygon: [ [ edge ] nullary ] times pop pop ;
slices: dup PI 2 * swap / swap ;
regpoly: slices polygon ;

triangle: 3 regpoly ;
square: 4 regpoly ;
pentagon: 5 regpoly ;
hexagon: 6 regpoly ;
circle: 36 regpoly ;

star: 144 deg2rad 5 polygon ;

shapes: begin [250 -425] go 500 triangle 500 square 500 pentagon 500 hexagon show ;

burst: begin 60 [ PI 30 / _i * head [0 0] go W 2 / move ] times show ;
squaril: begin [-425 -225] go 20 [ 850 move 126 deg2rad turn ] times show ;
spiro: begin [ W 25 / circle ] 15 spin show ;
stars: begin [ W 2 / star ] 3 spin show ;
rose: begin 0 102 [ 5 + dup move 84 deg2rad turn ] times show pop ;

petalArc: [ dup2 turn move ] times pop pop ;
petal:
  2 [
    28 6 deg2rad 16 petalArc
    7 -6 deg2rad 16 petalArc
    180 deg2rad turn
  ] times ;
flower: begin [ petal ] 15 spin show ;

spiral: begin 1 [ 750 > ] [] [ 5 + dup move 92 deg2rad turn ] tailrec show ;

nodeCanvas canvasContext pop
1000 1000 canvasDimensions
"#333" fillStyle
"rgba(255, 255, 255, 0.9)" strokeStyle
3 lineWidth

canvasWidth -> W
canvasHeight -> H
W 2 / -> WMax
H 2 / -> HMax
WMax neg -> WMin
HMax neg -> HMin

centerCanvas

clearCanvas
shapes
"images/shapes.png" writeImage

clearCanvas
burst
"images/burst.png" writeImage

clearCanvas
squaril
"images/squaril.png" writeImage

clearCanvas
spiro
"images/spiro.png" writeImage

clearCanvas
stars
"images/stars.png" writeImage

clearCanvas
rose
"images/rose.png" writeImage

clearCanvas
flower
"images/flower.png" writeImage

clearCanvas
spiral
"images/spiral.png" writeImage
